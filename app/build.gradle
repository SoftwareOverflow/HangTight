plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def localProperties = new Properties()
try {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
} catch (Exception ignored) {
    logger.warn("No Local Properties File Found!")
}

android {
    namespace 'com.softwareoverflow.hangtight'
    compileSdk 33

    defaultConfig {
        applicationId "com.newtonapps.hangtight"
        minSdk 27
        targetSdk 33
        versionCode 37
        versionName "2.5.4"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        if (!localProperties['base64EncodedPublicKey']) {
            logger.warn('base64EncodedPublicKey must be set for signature verification. ' +
                    'You can add it to local.properties once you have gotten it from Play. ' +
                    'Example: base64EncodedPublicKey=[key value]')
        }
        buildConfigField("String", "BASE64_ENCODED_PUBLIC_KEY", "\"" + localProperties['base64EncodedPublicKey'] + "\"")
        buildConfigField("String[]", "DEV_DEVICES", localProperties['testDeviceIds'])
    }

    buildTypes {
        debug {
            resValue "string", "adUnitId_banner", "\"ca-app-pub-3940256099942544/6300978111\""
            resValue "string", "adUnitId_interstitial", "\"ca-app-pub-3940256099942544/1033173712\""
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            resValue "string", "adUnitId_banner", "\"ca-app-pub-5961771507160254/4534904901\""
            resValue "string", "adUnitId_interstitial", "\"ca-app-pub-5961771507160254/2482816006\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def nav_version = "2.5.3"
    def roomVersion = "2.4.2"
    def coroutinesVersion = "1.6.1"
    def composeVersion = "1.5.0"
    def hiltVersion = "2.47"

    implementation "androidx.core:core-ktx:1.10.1"
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.0')
    implementation platform('androidx.compose:compose-bom:2022.10.00')

    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"


    implementation "androidx.navigation:navigation-fragment-ktx:2.5.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.3"
    implementation "androidx.navigation:navigation-compose"
    implementation "androidx.activity:activity-compose"
    //implementation "androidx.navigation:navigation-compose"
    //implementation "androidx.navigation:navigation-ui-ktx"

    implementation 'androidx.preference:preference:1.2.0'

    implementation 'com.google.android.gms:play-services-ads:20.6.0'

    // Monetization
    implementation 'com.android.billingclient:billing-ktx:6.0.1'

    // Testing
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.5.4"
    testImplementation 'androidx.test:core-ktx:1.5.0'
    testImplementation 'androidx.test:runner:1.5.2'

    debugImplementation "androidx.compose.ui:ui-tooling"

    // Logging
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Firebase (with BoM)
    implementation platform('com.google.firebase:firebase-bom:30.0.1')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // In-app reviews
    implementation("com.google.android.play:review:2.0.0")
    implementation("com.google.android.play:review-ktx:2.0.0")

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    // Room databases
    implementation "androidx.room:room-runtime:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"

    // ComposeDestinations library dependencies
    implementation 'io.github.raamcosta.compose-destinations:core:1.5.5-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.5.5-beta'

    // Hilt DI dependencies
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    ksp "com.google.dagger:hilt-android-compiler:$hiltVersion"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    implementation("com.google.android.ump:user-messaging-platform:2.1.0")
}