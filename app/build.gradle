apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

def localProperties = new Properties()
try {
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))
} catch (Exception ignored) {
    logger.warn("No Local Properties File Found!")
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.newtonapps.hangtight"
        minSdkVersion 24
        targetSdkVersion 32
        versionCode 22
        versionName "2.1.2"

        if (!localProperties['base64EncodedPublicKey']) {
            logger.warn('base64EncodedPublicKey must be set for signature verification. ' +
                    'You can add it to local.properties once you have gotten it from Play. ' +
                    'Example: base64EncodedPublicKey=[key value]')
        }
        buildConfigField("String", "BASE64_ENCODED_PUBLIC_KEY", "\"" + localProperties['base64EncodedPublicKey'] + "\"")
        buildConfigField("String[]", "DEV_DEVICES", localProperties['testDeviceIds'])
    }
    buildTypes {
        debug {
            resValue "string", "adUnitId_banner", "\"ca-app-pub-3940256099942544/6300978111\""
            resValue "string", "adUnitId_interstitial", "\"ca-app-pub-3940256099942544/1033173712\""
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "adUnitId_banner", "\"ca-app-pub-5961771507160254/4534904901\""
            resValue "string", "adUnitId_interstitial", "\"ca-app-pub-5961771507160254/2482816006\""
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    def roomVersion = "2.4.2"
    def coroutinesVersion = "1.6.1"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'com.google.android.gms:play-services-ads:20.6.0'
    implementation 'com.google.firebase:firebase-analytics:20.1.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.android.billingclient:billing:4.1.0'
    implementation 'com.android.billingclient:billing-ktx:4.1.0'
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"

    // Room databases
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
}
repositories {
    mavenCentral()
}
